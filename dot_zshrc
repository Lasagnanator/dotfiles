### ZINIT ###
# Bootstrap
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
if [[ ! -d "$ZINIT_HOME" ]]; then
    mkdir -p "$(dirname "$ZINIT_HOME")"
    git clone 'https://github.com/zdharma-continuum/zinit.git' "$ZINIT_HOME"
fi
source "${ZINIT_HOME}/zinit.zsh"

# Plugins
zinit light zsh-users/zsh-syntax-highlighting
zinit light zsh-users/zsh-completions
zinit light zsh-users/zsh-autosuggestions
zinit light Aloxaf/fzf-tab

# Snippets
zinit snippet OMZP::command-not-found
zinit snippet OMZP::eza
zinit snippet OMZP::sudo

### WSL ###
if [[ -v WSLENV ]]; then
    export PATH="${PATH}:/mnt/c/Windows/:/mnt/c/Windows/System32:/mnt/c/Windows/System32/WindowsPowerShell/v1.0"
    keep_current_path() {
        printf "\e]9;9;%s\e\\" "$(wslpath -w "$PWD")"
    }
    precmd_functions+=(keep_current_path)
    DOCKER_COMP_PATH="${HOME}/.docker/completions"
    if command -v docker >/dev/null 2>&1; then
        if [[ ! -s "${DOCKER_COMP_PATH}/_docker" ]]; then
            mkdir -p "${DOCKER_COMP_PATH}"
            docker completion zsh > "${DOCKER_COMP_PATH}/_docker"
        fi
        FPATH="${FPATH}:${DOCKER_COMP_PATH}"
    fi
fi

### COMPLETION ###
autoload -Uz compinit && compinit
zinit cdreplay -q
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' menu no
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'ls --color $realpath'
zstyle ':fzf-tab:complete:__zoxide_z:*' fzf-preview 'ls --color $realpath'

### OPTIONS ###
setopt autocd
setopt correct
unsetopt beep

### HISTORY ###
export HISTSIZE=5000
export HISTFILE="${HOME}/.histfile"
export SAVEHIST="${HISTSIZE}"
export HISTDUP=erase
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt hist_ignore_dups
setopt hist_find_no_dups

### KEYBINDS ###
bindkey -e
# bindkey '^[w' kill-region
bindkey '^[s' sudo-command-line # Alt-S
bindkey '^u' backward-kill-line # Ctrl-U
bindkey "^[[3~" delete-char     # Delete
bindkey '^[[1;5C' forward-word  # Ctrl-right
bindkey '^[[1;5D' backward-word # Ctrl-left
bindkey '^H' backward-kill-word # Ctrl-Delete
bindkey '^[[3;5~' kill-word     # Ctrl-Delete
autoload -Uz edit-command-line &&
    zle -N edit-command-line &&
    bindkey '^[e' edit-command-line # Alt-e

### ENVS ###
export WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'
export EDITOR='nvim'
export VISUAL='nvim'
export PATH="${PATH}:${HOME}/.local/bin:${HOME}/.scripts"
export COLORTERM='truecolor'

### ALIASES ###
# Colors
alias grep='grep --color'
alias diff='diff --color=auto'
alias ip='ip -color=auto'

# Utilities
alias v='nvim'
alias vi='nvim'
alias vim='nvim'
alias sv='sudo -E nvim'
alias g='git'
alias r='ranger-cd'
alias d='docker'
alias k='kubectl'
alias t='task'
alias tf='terraform'
alias lz='lazygit'
alias ldk='lazydocker'
alias bt='bluetoothctl'
alias cm='chezmoi'
alias lt='eza -T'
alias ltl='eza -T --level'
alias open='xdg-open'
alias venv='activate_venv'
alias srsync='rsync --rsync-path="sudo rsync"'

# Packet managers
alias pac='sudo pacman'
alias zyp='sudo zypper'
alias dnf='sudo dnf'
alias apt='sudo apt'

### FUNCTIONS ###
reload() {
    echo -ne '\033[1mReloading configuration... '
    source "${HOME}/.zshrc"
    echo -e '\033[1;32mDone!\n'
}

activate_venv() {
    local GIT_REPO CURRENT_DIR
    GIT_REPO="$(git rev-parse --show-toplevel 2>/dev/null)"
    CURRENT_DIR="${GIT_REPO:-$PWD}"
    if [[ -f "${CURRENT_DIR}/.venv/bin/activate" ]]; then
        source "${CURRENT_DIR}/.venv/bin/activate"
        echo -e '\033[1;32mLoaded virtual environment!'
    else
        echo -e "\\033[1;31mNo virtual environment found in ${CURRENT_DIR}!"
    fi
}

### PER-ENVIRONMENT CONFIGURATION ###
if [[ ! -d "${HOME}/.zshrc.d" ]]; then
    mkdir -p "${HOME}/.zshrc.d"
fi
for rc in "${HOME}/.zshrc.d/"*(N); do
    if [[ -f "${rc}" ]]; then
        source "${rc}"
    fi
done
unset rc

### INTEGRATIONS ###
command -v fzf >/dev/null 2>&1 && eval "$(fzf --zsh)"
command -v zoxide >/dev/null 2>&1 && eval "$(zoxide init zsh)"
command -v direnv >/dev/null 2>&1 && eval "$(direnv hook zsh)"
command -v thefuck >/dev/null 2>&1 && eval "$(thefuck --alias)"
# command -v starship >/dev/null 2>&1 && eval "$(starship init zsh)"
command -v oh-my-posh >/dev/null 2>&1 && eval "$(oh-my-posh init zsh --config ~/.config/oh-my-posh/config.toml)"

### KUBECTL ###
if command -v kubectl >/dev/null 2>&1; then
    KUBECONFIGS_PATH="${HOME}/.kube/config.d/"
    [[ ! -d "${KUBECONFIGS_PATH}" ]] && mkdir -p "${KUBECONFIGS_PATH}"
    KUBECONFIG="$(find "${KUBECONFIGS_PATH}" -type f | tr '\n' ':')"
    export KUBECONFIG
fi

### KREW ###
export KREW_DIR="${KREW_ROOT:-$HOME/.krew}"
[[ -d "$KREW_DIR" ]] && export PATH="${PATH}:${KREW_DIR}/bin"

### NVM ###
export NVM_DIR="$HOME/.nvm"
[[ -s "$NVM_DIR/nvm.sh" ]] && source "$NVM_DIR/nvm.sh"

### SDKMAN ###
export SDKMAN_DIR="$HOME/.sdkman"
[[ -s "$SDKMAN_DIR/bin/sdkman-init.sh" ]] && source "$SDKMAN_DIR/bin/sdkman-init.sh"

